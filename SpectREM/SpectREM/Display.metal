//
//  Display.metal
//  SpectREM
//
//  Created by Mike on 20/07/2018.
//  Copyright Â© 2018 71Squared Ltd. All rights reserved.
//

#include <metal_stdlib>
#include <simd/simd.h>

using namespace metal;

#import "ShaderTypes.h"

// Vertex shader outputs and per-fragment inputs. Includes clip-space position and vertex outputs
//  interpolated by rasterizer and fed to each fragment generated by clip-space primitives.
typedef struct
{
    float4 clipSpacePosition [[position]];
    float2 textureCoordinate;
    
} RasterizerData;

///////////////////////////////////////////////////////////////////////////////////////
// Distortion used to give the screen a curved effect
///////////////////////////////////////////////////////////////////////////////////////
vector_float2 radialDistortion(vector_float2 pos, float distortion)
{
    vector_float2 cc = pos - vector_float2(0.5, 0.5);
    float dist = dot(cc, cc) * distortion;
    return (pos + cc * (0.5 + dist) * dist);
}

///////////////////////////////////////////////////////////////////////////////////////
// colorCorrection used to adjust the saturation, constrast and brightness of the image
///////////////////////////////////////////////////////////////////////////////////////
vector_float3 colorCorrection(vector_float3 color, float saturation, float contrast, float brightness)
{
    const vector_float3 meanLuminosity = vector_float3(0.5, 0.5, 0.5);
    const vector_float3 rgb2greyCoeff = vector_float3(0.2126, 0.7152, 0.0722);    // Updated greyscal coefficients for sRGB and modern TVs
    
    vector_float3 brightened = color * brightness;
    float intensity = dot(brightened, rgb2greyCoeff);
    vector_float3 saturated = mix(vector_float3(intensity), brightened, saturation);

    return mix(meanLuminosity, saturated, contrast);
}

///////////////////////////////////////////////////////////////////////////////////////
// Split the red, green and blue channels of the texture passed in
///////////////////////////////////////////////////////////////////////////////////////
vector_float4 channelSplit(texture2d<float>image, sampler tex, vector_float2 coord, float spread){
    vector_float4 frag;
    frag.r = image.sample(tex, vector_float2(coord.x - spread, coord.y)).r;
    frag.g = image.sample(tex, vector_float2(coord.x, coord.y)).g;
    frag.b = image.sample(tex, vector_float2(coord.x + spread, coord.y)).b;
    frag.a = 1.0;
    return frag;
}

/**
 VERTEX SHADER
 Vertex shader used to pass the vertices through to the fragment shader. Not much going on in here :o)
 **/
vertex RasterizerData vertexShader(uint vertexID [[ vertex_id ]],
                                   constant Vertex *vertexArray [[ buffer(VertexInputIndexVertices) ]],
                                   constant vector_uint2 *viewportSizePointer  [[ buffer(VertexInputIndexViewportSize) ]])
{
    RasterizerData out;
    float2 pixelSpacePosition = vertexArray[vertexID].position.xy;
    out.clipSpacePosition.xy = pixelSpacePosition;
    out.clipSpacePosition.z = 0.0;
    out.clipSpacePosition.w = 1.0;
    out.textureCoordinate = vertexArray[vertexID].textureCoordinate;
    
    return out;
}

/**
 COLOUR LOOKUP FRAGMENT SHADER
 Used to lookup the read colours to use from the packed image data provided by the emaultor. The emulator
 output provides a single byte per pixel and the value of that byte is used to lookup the actual colour from the colour
 lookup texture
 **/
fragment vector_float4 clutShader( RasterizerData in [[stage_in]],
                           texture2d<half> colorTexture [[ texture(TextureIndexPackedDisplay) ]],
                           texture1d<float> clutTexture [[ texture(TextureIndexCLUT) ]])
{
    constexpr sampler textureSampler (mag_filter::nearest,
                                      min_filter::nearest);

    const float paletteColors = 16.0;
    const float clutUVAdjust = 1.0 / paletteColors;
    const int colorSample = colorTexture.sample(textureSampler, in.textureCoordinate)[0] * 256;
    return clutTexture.sample(textureSampler, colorSample * clutUVAdjust);
}

/**
 EFFECTS FRAGMENT SHADER
 Used to apply effects to the output from the colour lookup shader. The output from this shader is what is
 then displayed on screen
 **/
fragment vector_float4 effectsShader(RasterizerData in [[stage_in]],
                              texture2d<float> colorTexture [[ texture(0) ]],
                              constant Uniforms & uniforms [[buffer(0) ]])
{
    constexpr sampler textureSampler (mag_filter::linear,
                                      min_filter::linear);
    
    float max = pow(uniforms.displayVignetteX, uniforms.displayVignetteY);
    const float w = 32 + 256 + 32;
    const float h = 32 + 192 + 32;
    float border = 32 - uniforms.displayBorderSize;
    float new_w = w - (border * 2);
    float new_h = h - (border * 2);
    vector_float4 fragColor;

    vector_float2 texCoord = radialDistortion(in.textureCoordinate, uniforms.displayCurvature);
    vector_float2 scanTexCoord = texCoord;

    // Anything outside the texture should be black, otherwise sample the texel in the texture
    if (texCoord.x < 0 || texCoord.y < 0 || texCoord.x > 1 || texCoord.y > 1)
    {
        fragColor = vector_float4(0.0, 0.0, 0.0, 1);
    }
    else
    {
        // Update the UV coordinates based on the size of the border
        float u = (texCoord.x * new_w) + border;
        float v = ((texCoord.y * new_h) + border);

        // Apply pixel filtering
        vector_float2 vUv = vector_float2(u, v);
        float alpha = uniforms.displayPixelFilterValue; // 0.5 = Linear, 0.0 = Nearest
        vector_float2 x = fract(vUv);
        vector_float2 x_ = clamp(0.5 / alpha * x, 0.0, 0.5) + clamp(0.5 / alpha * (x - 1.0) + 0.5, 0.0, 0.5);
        texCoord = (floor(vUv) + x_) / vector_float2(w, h);

        // Apply RGB shift if necessary, otherwise just take the colour from the texture as is
        if (uniforms.displayRGBOffset > 0)
        {
            fragColor = channelSplit( colorTexture, textureSampler, texCoord, uniforms.displayRGBOffset);
        }
        else
        {
            // Grab the color from the texture
            fragColor = colorTexture.sample( textureSampler, texCoord );
        }
        
        fragColor.rgb = colorCorrection(fragColor.rgb, uniforms.displaySaturation, uniforms.displayContrast, uniforms.displayBrightness);
        
        // Add scanlines
        float scanline = sin(scanTexCoord.y * uniforms.displayScanlineSize) * 0.09 * uniforms.displayScanlines;
        fragColor.rgb -= scanline;
        
        // Add the vignette which adds a shadow and curving to the corners of the screen. Do this after applying the scan lines so
        // they are faded out into the shadow as well
        if (uniforms.displayShowVignette == 1)
        {
            float vignette = scanTexCoord.x * scanTexCoord.y * (1.0 - scanTexCoord.x) * (1.0 - scanTexCoord.y);
            fragColor.rgb *= smoothstep(0.0, max, vignette);
        }
    }

    // We return the color of the texture
    return fragColor;
}
